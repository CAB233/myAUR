# Maintainer: cubercsl <2014cais01 at gmail dot com>
# Maintainer: Integral <integral at member dot fsf dot org>

pkgname=dae
pkgver=0.9.0 # renovate: datasource=custom.nvchecker depName=dae
pkgrel=1
pkgdesc="A Linux lightweight and high-performance transparent proxy solution based on eBPF."
arch=('x86_64')
url="https://github.com/daeuniverse/dae"
license=('AGPL-3.0-or-later')
depends=('glibc' 'v2ray-geoip-ly' 'v2ray-geosite-ly')
makedepends=('clang' 'go' 'git')
provides=("dae")
backup=("etc/dae/config.dae")
source=("git+https://github.com/daeuniverse/dae.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
    cd "$srcdir"/$pkgname
    git submodule update --init --recursive
}

build() {
    export CFLAGS="-fno-stack-protector"
    export CGO_ENABLED=1
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export BUILD_ARGS='
        -buildmode=pie \
        -trimpath \
        -ldflags=-linkmode=external \
        -mod=readonly \
        -modcacherw \
        -ldflags "-s -w -X github.com/daeuniverse/dae/cmd.Version=$(VERSION) \
            -X github.com/daeuniverse/dae/common/consts.MaxMatchSetLen_=$(MAX_MATCH_SET_LEN)"'
    cd "$srcdir"/$pkgname
    make VERSION=$pkgver
}

package() {
    install -Dvm755 "$srcdir"/$pkgname/dae "$pkgdir"/usr/bin/dae
    install -Dvm644 "$srcdir"/$pkgname/install/dae.service -t "$pkgdir"/usr/lib/systemd/system
    install -Dvm600 "$srcdir"/$pkgname/example.dae "$pkgdir"/etc/dae/config.dae

    mkdir -pv "$pkgdir"/usr/share/$pkgname
    ln -sv /usr/share/v2ray/{geoip.dat,geosite.dat} "$pkgdir"/usr/share/dae

    install -Dvm644 "$srcdir"/$pkgname/install/shell-completion/dae.bash "$pkgdir"/usr/share/bash-completion/completions/dae
    install -Dvm644 "$srcdir"/$pkgname/install/shell-completion/dae.zsh "$pkgdir"/usr/share/zsh/site-functions/_dae
    install -Dvm644 "$srcdir"/$pkgname/install/shell-completion/dae.fish "$pkgdir"/usr/share/fish/vendor_completions.d/dae.fish
}
