# Maintainer: yidaduizuoye <yidaduizuoye at outlook dot com>

pkgname="grub-theme-minegrub"
_pkgname="minegrub-theme"
pkgver=3.1.0
pkgrel=2
pkgdesc="A Grub Theme in the style of Minecraft"
arch=('any')
url="https://github.com/Lxtharia/minegrub-theme"
license=('MIT')
depends=('grub')
makedepends=('git')
optdepends=('fastfetch: Updating splash text, background and "x Packages Installed" text after every boot'
            'python: Updating splash text, background and "x Packages Installed" text after every boot'
            'python-pillow: Updating splash text, background and "x Packages Installed" text after every boot')
source=("git+$url"
        "0001-fix-update-theme-script-path.patch"
        "0002-fix-cachedir-path.patch")
sha256sums=('SKIP'
            '78029e69f46000848a247b4dc61f05f26ffc2ff5e13a40e3e0e92011f27a8f2e'
            'bd2e560cd7e4361e1583a11991ce60a02f509aafb64e31d4225c88598babea0e')

prepare() {
    cd "$srcdir"/$_pkgname
    git checkout v$pkgver
    patch -b -l -d "$srcdir"/$_pkgname -Np1 -i "$srcdir"/0001-fix-update-theme-script-path.patch
    patch -b -l -d "$srcdir"/$_pkgname -Np1 -i "$srcdir"/0002-fix-cachedir-path.patch
}

package() {
    mkdir -p "$pkgdir"/usr/share/grub/themes/minegrub/{background_options,minegrub}
	cp -r "$srcdir"/$_pkgname/background_options/* "$pkgdir"/usr/share/grub/themes/minegrub/background_options
	cp -r "$srcdir"/$_pkgname/minegrub/* "$pkgdir"/usr/share/grub/themes/minegrub/minegrub
    install -Dm755 "$srcdir"/$_pkgname/choose_background.sh -t "$pkgdir"/usr/share/grub/themes/minegrub
    install -Dm644 "$srcdir"/$_pkgname/minegrub-update.service -t "$pkgdir"/usr/lib/systemd/system
    install -Dm644 "$srcdir"/$_pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$_pkgname
}
