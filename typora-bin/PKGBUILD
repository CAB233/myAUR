# Maintainer: Jonas BÃ¶gle <aur@iwr.sh>
# Contributor: Jonathan Duck <duckbrain30@gmail.com>

pkgname=typora-bin
_pkgname=typora
pkgver=1.10.7 # renovate: datasource=custom.nvchecker depName=typora
pkgrel=1
pkgdesc="A minimal markdown editor and reader, but with some hooks"
arch=('x86_64')
license=('custom:"Copyright (c) 2015 Abner Lee All Rights Reserved."' 'MIT')
url="https://typora.io/"
conflicts=('typora' 'typora-free' 'typora-electron')

depends=('gtk3' 'nss' 'alsa-lib')
makedepends=('rust' 'git')
optdepends=(
	'noto-fonts-emoji: Or some other emoji font to see emojis'
	'pandoc: Import/export for extra file formats')

source=("https://download.typora.io/linux/typora_${pkgver}_amd64.deb"
		"typora.sh"
		"git+https://github.com/DiamondHunters/NodeInject.git"
		"git+https://github.com/DiamondHunters/NodeInject_Hook_example.git")
sha512sums=('917d1c52edb0833e9eb5336587dcb49fcdeff2aac9fb720c1be6d9893ca47c3fcd59d0e4f390e058a216032ac7168c27fce4fa621c35453b4805b594be2506de'
            '2dce26e1c041ffe56f83cc53d785716f9835189d984120f117e7d427f6167442afc4be891ea31131758856e2df9f8c946c7d409886b1d4f4f3b1a80b278092de'
            'SKIP'
            'SKIP')

install=${pkgname}.install

build() {
	# Build keygen
	cd "${srcdir}/NodeInject_Hook_example/license-gen"
	cargo build --release

	# Build NodeInject
	git checkout --quiet 81f86127d97a66f9279f0db705ae6708ca06b1f9
	cd "${srcdir}/NodeInject"
	cp "${srcdir}/NodeInject_Hook_example/hook.js" "${srcdir}/NodeInject/src/hooklog.js"
	cargo build --release
}

package() {
	install -d "${pkgdir}/opt/$_pkgname"
	bsdtar -xf data.tar.zst -C "${pkgdir}/"

	# remove lintian overrides
	rm -rf "${pkgdir}/usr/share/lintian/"
	# replace bin link with custom launch script
	rm -rf "${pkgdir}/usr/bin/$_pkgname"
	install -m755 "${srcdir}/$_pkgname.sh" "${pkgdir}/usr/bin/$_pkgname"

	# move license to correct path
	install -Dm644 "${pkgdir}/usr/share/doc/$_pkgname/copyright" "${pkgdir}/usr/share/licenses/$_pkgname/LICENSE"
	rm "${pkgdir}/usr/share/doc/$_pkgname/copyright"
	rmdir --ignore-fail-on-non-empty "${pkgdir}/usr/share/doc/$_pkgname" "${pkgdir}/usr/share/doc"

	# remove change log from application comment
	sed -i '/Change Log/d' "${pkgdir}/usr/share/applications/$_pkgname.desktop"
	chmod 644 "${pkgdir}/usr/share/applications/$_pkgname.desktop"
	cp -r "${pkgdir}/usr/share/$_pkgname/"* "${pkgdir}/opt/$_pkgname"
	rm -rf "${pkgdir}/usr/share/$_pkgname"

	# patch typora
	install -m755 "${srcdir}/NodeInject/target/release/node_inject" "${pkgdir}/opt/$_pkgname/node_inject"
	cd "${pkgdir}/opt/$_pkgname" && ./node_inject
	rm -rf "${pkgdir}/opt/$_pkgname/node"
	install -m755 "${srcdir}/NodeInject_Hook_example/license-gen/target/release/license-gen" "${pkgdir}/usr/bin/typora-license-gen"

	# fix permissions
	find "${pkgdir}" -type d -exec chmod 755 {} \;
}
