# Maintainer: yidaduizuoye <yidaduizuoye at outlook dot com>

pkgname=sing-box-reF1nd
_pkgname=sing-box
pkgver=1.12.0rc.4 # renovate: datasource=custom.nvchecker depName=sing-box-reF1nd
pkgrel=1
_pkgver=$(echo "${pkgver}" | sed 's/\([0-9]\+\.[0-9]\+.[0-9]\+\)\(alpha\|beta\|rc\)/\1-\2/')
pkgdesc='The universal proxy platform.'
arch=('x86_64')
url='https://sing-box.sagernet.org/'
license=('custom:GPL-3.0-or-later WITH name use or association addition')
depends=(
    'glibc'
)
makedepends=(
    'git'
    'go'
)
source=("git+https://github.com/reF1nd/sing-box.git#tag=v${_pkgver}-reF1nd")
sha256sums=('dcbe5c6d96c9a284c597d9821d1745aed49998cd2dcfd65582503e426caca780')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
backup=("etc/${_pkgname}/config.json")

build() {
    local tags=(
        'with_quic'
        'with_grpc'
        'with_dhcp'
        'with_wireguard'
        'with_utls'
        'with_acme'
        'with_clash_api'
        'with_v2ray_api'
        'with_gvisor'
        'with_embedded_tor'
        'with_tailscale'
    )
    local ldflags=(
        "-s"
        "-w"
        "-linkmode=external"
        "-X github.com/sagernet/sing-box/constant.Version=${_pkgver}"
    )

    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_ENABLED=1

    cd "${_pkgname}"
    go build \
        -v \
        -tags "${tags[*]}" \
        -ldflags "${ldflags[*]}" \
        ./cmd/sing-box

    install -d completions
    go run ./cmd/sing-box completion bash > completions/bash
    go run ./cmd/sing-box completion fish > completions/fish
    go run ./cmd/sing-box completion zsh > completions/zsh
}

package() {
    cd "${_pkgname}"
    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -Dm755 "${_pkgname}" -t "${pkgdir}/usr/bin"
    install -Dm644 "release/config/config.json" -t "${pkgdir}/etc/${_pkgname}"
    install -Dm644 "release/config/${_pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system"
    install -Dm644 "release/config/${_pkgname}@.service" -t "${pkgdir}/usr/lib/systemd/system"
    install -Dm644 "release/config/${_pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${_pkgname}.conf"
    install -Dm644 "release/config/${_pkgname}.rules" -t "${pkgdir}/usr/share/polkit-1/rules.d"
    install -Dm644 "release/config/${_pkgname}-split-dns.xml" "${pkgdir}/usr/share/dbus-1/system.d/${_pkgname}-split-dns.conf"

    install -Dm644 completions/bash "${pkgdir}/usr/share/bash-completion/completions/${_pkgname}.bash"
    install -Dm644 completions/fish "${pkgdir}/usr/share/fish/vendor_completions.d/${_pkgname}.fish"
    install -Dm644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_${_pkgname}"
}
