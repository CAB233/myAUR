# Maintainer: Luis Sarmiento < Luis.Sarmiento-ala-nuclear.lu.se >
pkgname='geant4'
pkgver=11.3.0
_pkgver=v${pkgver}
pkgrel=1
pkgdesc="A simulation toolkit for particle physics interactions."
# https://geant4-userdoc.web.cern.ch/UsersGuides/InstallationGuide/html/gettingstarted.html
depends=('cmake>=3.16'
         'expat>=2.0.1'
         'xerces-c>=3'
         'glu'
         'openmotif'
         'qt6-base' # soqt depends on qt6-base   
         'soqt' # this depends on qt6-base anyway
         'zlib>=1.2.3'
         'python>=3'
         'boost')
conflicts=('geant4_devel')
optdepends=('java-environment: for histogram visualizations and analysis'
            'tcsh: for C Shell support')
url="https://geant4.cern.ch/"
arch=('x86_64')
license=('custom: https://geant4.cern.ch/license/')
options=('!emptydirs')
source=("https://geant4-data.web.cern.ch/releases/${pkgname}-${_pkgver}.tar.gz")
sha256sums=('1da4318b3f96f87f4d47558a32dab269b8f3fc956708038c28e72a180b0efba6')

build() {
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  env -i \
      QT_SELECT=6 \
      PATH=/usr/bin \
      cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DGEANT4_USE_G3TOG4=ON \
        -DGEANT4_USE_GDML=ON \
        -DGEANT4_USE_INVENTOR_QT=ON \
        -DGEANT4_USE_OPENGL_X11=ON \
        -DGEANT4_USE_QT=ON \
        -DGEANT4_USE_RAYTRACER_X11=ON \
        -DGEANT4_USE_XM=ON \
        -DGEANT4_USE_SYSTEM_ZLIB=ON \
        -DGEANT4_INSTALL_PACKAGE_CACHE=OFF \
        -DGEANT4_BUILD_TLS_MODEL=global-dynamic \
        -DGEANT4_USE_QT_QT6=ON \
        -DGEANT4_USE_PYTHON=ON \
        -DGEANT4_INSTALL_DATA=ON \
        -DGEANT4_INSTALL_DATASETS_TENDL=ON \
        ../${pkgname}-${_pkgver}

  G4VERBOSE=1 make
}

package() {
  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install
}
