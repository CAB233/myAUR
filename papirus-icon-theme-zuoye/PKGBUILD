# Maintainer: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Mohammadreza Abdollahzadeh <morealaz at gmail dot com>
# Contributor: Edgard Castro <castro@edgard.org>
# Contributor: Frederic Bezies <fredbezies at gmail dot com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Icaro Perseo <icaroperseo[at]protonmail[dot]com>
# Contributor: Lucas Sali√©s Brum <lucas@archlinux.com.br>

pkgname=papirus-icon-theme-zuoye
pkgver=20250201
pkgrel=1
pkgdesc="Pixel perfect icon theme for Linux (with some patches)"
arch=('any')
url="https://github.com/PapirusDevelopmentTeam/papirus-icon-theme"
license=('GPL-3.0-or-later')
depends=('gtk-update-icon-cache')
makedepends=('git')
provides=("papirus-icon-theme")
conflicts=(
    "papirus-icon-theme"
    "papirus-icon-theme-git"
)
source=(
    "git+${url}#tag=${pkgver}"
    "fix-telegram-desktop-icon-names.patch::${url}/commit/f57bf280295494bd2f3f031ca65bb958e7f97da5.patch"
    #"redesign-WeChat-icon.patch::${url}/commit/aab94caeb466f402e0cbb263c80caeed1ae3105d.patch"
)
sha256sums=('839a87b6159bc9f411f93d1c4766b4b0bb6ef05014f662e99e3f0cec888eafe3'
            '63fad7824c64af77f4e1eff41a04ee7cb8f85d587421848e20b2aab232d01b73')
options=('!strip')

prepare() {
    patch -d "${srcdir}/papirus-icon-theme" -p1 < "fix-telegram-desktop-icon-names.patch"
    #patch -d "${srcdir}/papirus-icon-theme" -p1 < "redesign-WeChat-icon.patch"
}

package() {
    cd "${srcdir}/papirus-icon-theme"
    make DESTDIR="$pkgdir" ICON_THEMES="Papirus Papirus-Dark Papirus-Light" install
}
