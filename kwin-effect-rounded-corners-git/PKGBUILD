# Maintainer:
# Contributor: 2xsaiko <aur@dblsaiko.net>

pkgname="kwin-effect-rounded-corners-git"
pkgver=0.7.2.r48.g284647c
pkgrel=1
pkgdesc="Rounds the corners of your windows (wayland)"
arch=('x86_64')
url="https://github.com/matinlotfali/KDE-Rounded-Corners"
license=("GPL-3.0-only")
depends=(
  'kwin'
)
makedepends=(
  'cmake'
  'extra-cmake-modules'
  'git'
  'ninja'
)
source=("kde-rounded-corners"::"git+$url.git")
sha256sums=('SKIP')

prepare() {
  # ensure Qt6
  sed -E -e 's&\bQUIET\b&REQUIRED&' -i "kde-rounded-corners/cmake/qtversion.cmake"
}

pkgver() {
  cd kde-rounded-corners
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' \
    | sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  local _cmake_options=(
    -B build
    -S "kde-rounded-corners"
    -G Ninja
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build

  DESTDIR="fakeinstall" cmake --install build
}

package() {
  cp -a fakeinstall/* "$pkgdir/"
  rm -rf "$pkgdir/usr/lib/qt6/plugins/kwin-x11/"
  chmod -R u+rwX,go+rX,go-w "$pkgdir/"
}
