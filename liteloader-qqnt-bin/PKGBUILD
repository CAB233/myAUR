# Maintainers: kobe-koto <admin[at]koto.cc>, Ketal_Q_ray <k[at]ketal.icu>
# Maintainer: yidaduizuoye <yidaduizuoye@outlook.com>

pkgname="liteloader-qqnt-bin"
_pkgname="LiteLoaderQQNT"
pkgver=1.2.0 # renovate: datasource=github-tags depName=LiteLoaderQQNT/LiteLoaderQQNT
pkgrel=2
pkgdesc="轻量, 简洁, 开源的 QQNT 插件加载器"
arch=('any')
url="https://github.com/LiteLoaderQQNT/LiteLoaderQQNT"
license=('MIT')
depends=("linuxqq")
conflicts=("linuxqq-appimage" "liteloader-qqnt")
provides=("liteloader-qqnt")
install="${pkgname}.install"
source=("${url}/releases/download/${pkgver}/${_pkgname}.zip"
		"liteloader-qqnt-depatch.hook"
		"liteloader-qqnt-patch.hook")

sha256sums=('93c8520eb7cf26d2b8e06df70dc1c0d831734498dc9fd3767c099b4fbd89f5c0'
            '6bee3d5f5f238ad42ae14253eff43e821726e68f62d22e511d9dc48522aef9d0'
            '372a9f6faa4589fa93297171b0ed45d171e6fc6d6ae1767d521af4081165bf40')

package() {
	# prepare to copy files
	mkdir -p "${pkgdir}/opt/LiteLoader"
	mkdir -p "${pkgdir}/opt/QQ/resources/app/application"

	# copy files
	cp -rf "${srcdir}/"* "${pkgdir}/opt/LiteLoader/"
	cp -f "${srcdir}/src/preload.js" "${pkgdir}/opt/QQ/resources/app/application/preload.js"

	# modify premissions
	chmod -R 0755 "${pkgdir}/opt/LiteLoader"

	# clean up in target dir
	rm -f "${pkgdir}/opt/LiteLoader/LiteLoaderQQNT.zip"
	rm -f "${pkgdir}/opt/LiteLoader/liteloader-qqnt-patch.hook"
	rm -f "${pkgdir}/opt/LiteLoader/liteloader-qqnt-depatch.hook"

	# install hooks
	install -Dm644 "${srcdir}/liteloader-qqnt-patch.hook" "${pkgdir}/etc/pacman.d/hooks/liteloader-qqnt-patch.hook"
	install -Dm644 "${srcdir}/liteloader-qqnt-depatch.hook" "${pkgdir}/etc/pacman.d/hooks/liteloader-qqnt-depatch.hook"
}
