# Maintainers: kobe-koto <admin[at]koto.cc>, Ketal_Q_ray <k[at]ketal.icu>
# Maintainer: yidaduizuoye <yidaduizuoye@outlook.com>

pkgname="liteloader-qqnt-bin"
_pkgname="LiteLoaderQQNT"
pkgver=1.2.1 # renovate: datasource=github-tags depName=LiteLoaderQQNT/LiteLoaderQQNT
pkgrel=6
pkgdesc="轻量, 简洁, 开源的 QQNT 插件加载器"
arch=('any')
url="https://github.com/LiteLoaderQQNT/LiteLoaderQQNT"
license=('MIT')
depends=("linuxqq" "bubblewrap")
optdepends=("linuxqq-patch: 固定 MAC 地址、适配 LiteLoaderQQNT 框架")
conflicts=("liteloader-qqnt-git" "linuxqq-nt-bwrap")
source=("${url}/releases/download/${pkgver}/${_pkgname}.zip"
		"liteloader-qqnt-patch.hook"
		"liteloader-qqnt-depatch.hook"
		"gen_preload.js"
		"index.js"
		"patch_liteloader_bwrap.sh")

sha256sums=('affbd02573900d4f5743e33386d41b38adaac6ed9a130d57e7c8c75f8dc68838'
            'ca9e747de8a659132e27a1c454863b5ac736356396787634abe360255607e49f'
            '9d2ea72bb35ba91b58acdd4b1742a5a0ffae270aa609da225626a2e9b4ff578f'
            '6105389087a7d94eb743191aa1bf484bdf48f24d6470a8c1e4e7a74aa359ec23'
            'da197eee75d92d5d79861d3531e59afc1caf091446b4ba2d0a8916e0a9c87b0b'
            'c235afd0a8bbb0c0dc5fe59b35934802fe277eec79996a2a6d26f2ab41ce0e73')

package() {
	# 准备文件夹
	mkdir -p "${pkgdir}/opt/LiteLoaderQQNT/src"
	mkdir -p "${pkgdir}/opt/LiteLoaderQQNT/patching"
	mkdir -p "${pkgdir}/opt/LiteLoaderQQNT/plugins"

	# 移动文件到相应位置
	cp -rf "${srcdir}/src/"* "${pkgdir}/opt/LiteLoaderQQNT/src"
	install -Dm644 "${srcdir}/package.json" "${pkgdir}/opt/LiteLoaderQQNT/package.json"
	install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/$_pkgname/LICENSE"
	install -Dm644 "${srcdir}/gen_preload.js" "${pkgdir}/opt/LiteLoaderQQNT/patching/gen_preload.js"
	install -Dm644 "${srcdir}/index.js" "${pkgdir}/opt/LiteLoaderQQNT/patching/index.js"
	install -Dm644 "${srcdir}/patch_liteloader_bwrap.sh" "${pkgdir}/opt/LiteLoaderQQNT/patching/patch_liteloader_bwrap.sh"

	# 配置 pacman hook
	install -Dm644 "${srcdir}/liteloader-qqnt-patch.hook" "${pkgdir}/etc/pacman.d/hooks/liteloader-qqnt-patch.hook"
	install -Dm644 "${srcdir}/liteloader-qqnt-depatch.hook" "${pkgdir}/etc/pacman.d/hooks/liteloader-qqnt-depatch.hook" 
}
